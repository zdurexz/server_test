//===== rAthena Script =======================================
//= Buying Shop Scripts
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.1b
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Buying Shop licenses.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Added Catalog Magician. [Euphy]
//= 1.1a Moved Catalog Magician to Renewal path. [Euphy]
//= 1.1b Added missing weight checks. [Euphy]
//============================================================ 

// Black Marketeer (Buy Licenses - Non-Merchant Classes) :: qskill_buy_store
//============================================================ 
que_job01,68,84,1	script	Black Marketeer#Buying	881,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "[Mr. Jass]";
		mes "คุณโลภเกินไปแม้เทียบกับคนอย่างฉัน!";
		mes "ทำไมไม่ไปแบ่งเบากระเป๋าก่อน";
		close;
	}
	if (getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[Mr. Jass]";
		mes "เฮ้คุณได้ทำสัญญากับ Hugh แล้ว";
		mes "ฉันไม่มีธุระอะไรกับคุณ";
		close;
	}
	mes "[Mr. Jass]";
	mes "คุณต้องมีบางอย่างที่ไม่ดีเพื่อมาหาฉัน";
	mes "คุณต้องการอะไร?";
	next;
	switch(select("Bulk Buyer Shop License:คุณคือใคร?:ไม่มีอะไรไม่มีอะไร!!")) {
	case 1:
		mes "[Mr. Jass]";
		mes "ฉันรู้แล้ว!";
		mes "แน่นอนฉันทำให้เธอได้";
		mes "ของฉันดูเหมือนของแท้ที่พ่อค้าได้รับจาก Hugh คนนั้น!";
		next;
		mes "[Mr. Jass]";
		mes "และใบอนุญาตของฉันดีกว่า 'เพราะคุณไม่จำเป็นต้องเรียนรู้ทักษะใดๆ";
		mes "อยากได้กี่คน";
		next;
		mes "[Mr. Jass]";
		mes "ขอให้คุณรู้ว่าฉันสามารถทำมันในปริมาณเล็กน้อยได้ครั้งละไม่เกิน 10 ครั้ง";
		mes "ราคาอันละ 500 zeny";
		next;
		while(1) {
			input .@input;
			mes "[Mr. Jass]";
			if (.@input == 0) {
				mes "คุณไม่ต้องการใบอนุญาตเหล่านั้นหรือ?";
				close;
			} else if (.@input > 10) {
				mes "ฉันทำได้ครั้งละไม่เกิน 10 ครั้งคนเท่านั้นเอง";
				next;
			} else {
				mes "มันจะมีค่าใช้จ่าย "+(.@input*500)+" zeny.";
				if (Zeny < (.@input*500)) {
					mes "แต่คุณมีเงินไม่พอ";
					next;
					mes "[Mr. Jass]";
					mes "คุณไม่รู้พื้นฐานของธุรกิจหรือเปล่าทุกอย่างมีราคา";
					mes "ถ้าคุณต้องการอะไรคุณต้องจ่าย";
				} else {
					mes "ฮา...ฮา!";
					mes "คุณ Hugh ฉันจะเข้ารับช่วงธุรกิจใบอนุญาตของคุณคุณจะได้เห็น!";
					mes "*หัวเราะคิกคัก*";
					getitem 12548,.@input; //Buy_Market_Permit2
					set Zeny,Zeny-(.@input*500);
				}
				close;
			}
		}
	case 2:
		mes "[Mr. Jass]";
		mes "ฉันจากบ้านเกิดไปนานแล้ว";
		mes "มันไม่มีความหมายที่จะถามว่าฉันเป็นใครเพราะสิ่งที่ฉันเหลืออยู่ตอนนี้คือความเกลียดชังของฉัน";
		next;
		mes "[Mr. Jass]";
		mes "... ";
		mes "Hugh เป็นพ่อค้าที่ทุจริตและไม่มีจริยธรรมทางธุรกิจ";
		mes "จุดประสงค์เดียวในชีวิตของฉันคือการทำลาย Hugh";
		next;
		mes "[Mr. Jass]";
		mes "อ๊ะฉันดื่มมากเกินไปแล้ว...(*Hic*)";
		mes "นั่นเป็นเพียงการพูดของอะโลฮอล";
		mes "ได้โปรดลืมสิ่งที่ฉันพูดไป";
		close;
	case 3:
		mes "[Mr. Jass]";
		mes "เอาล่ะไม่ต้องตะโกน";
		mes "ปล่อยฉันไว้คนเดียวถ้าคุณไม่มีธุระกับฉัน";
		close;
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
alberta_in,58,52,4	script	Purchasing Team#Buying	59,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "- คุณไม่สามารถสนทนาหรือทำเควสได้เนื่องจากคุณบรรทุกสิ่งของมากเกินไป -";
		close;
	}
	mes "[Mr. Hugh]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "ฉันเป็น Hugh จากทีมจัดซื้อ";
			mes "วันนี้มีอะไรให้ช่วยไหม";
			next;
			if(select("ซื้อ Bulk Buyer Shop License:ออก") == 2) {
				mes "[Mr. Hugh]";
				mes "โปรดอย่าลังเลที่จะถามฉันว่าคุณต้องการใบอนุญาตสำหรับร้านค้าผู้ซื้อจำนวนมากหรือไม่";
				mes "มาอีกแล้ว~";
				close;
			}
			mes "[Mr. Hugh]";
			mes "ราคา 200 zeny สำหรับสิทธิ์การใช้งานร้านค้า Bulk Buyer Shop แต่ละใบและคุณสามารถซื้อได้ครั้งละไม่เกิน 50 ใบ";
			mes "ต้องมีใบอนุญาตกี่ใบ";
			next;
			while(1) {
				input .@input;
				mes "[Mr. Hugh]";
				if (.@input == 0) {
					mes "คุณได้ยกเลิกการซื้อขายแล้ว";
					mes "ขอให้มีความสุขมากๆ นะครับ";
					close;
				} else if (.@input > 50) {
					mes "โปรดป้อนค่าไม่เกิน 50";
					next;
				} else {
					mes "มันจะมีค่าใช้จ่าย "+(.@input*200)+" zeny สำหรับ "+.@input+" licenses.";
					if (Zeny < (.@input*200)) {
						mes "แต่ดูเหมือนคุณจะมีเงินไม่พอ";
					} else {
						mes "ขอบคุณที่อุดหนุนครับ";
						getitem 6377,.@input; //Buy_Market_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		} else {
			mes "ฉันเป็นฮิวจ์จากทีมจัดซื้อที่ Alberta Merchant Guild";
			mes "คุณ ... ";
			next;
			mes "[Mr. Hugh]";
			mes "รู้หรือไม่กิลด์ของเราได้ออกใบอนุญาตให้บุคคลทั่วไปสามารถซื้อสินค้าจากผู้อื่นได้";
			next;
			select("ฉันไม่เคยมีปัญหาในการซื้อของ ...");
			mes "[Mr. Hugh]";
			mes "ถูก แต่ลองคิดดู";
			mes "คุณไม่เคยลำบากในการซื้อจำนวนมากหรือ";
			mes "คุณต้องหาและคุยกับทุกคนที่มีไอเท็มที่คุณต้องการ";
			mes "ไม่สะดวกเลยใช่ไหม?";
			next;
			mes "[Mr. Hugh]";
			mes "เนื่องจากการซื้อสินค้าจำนวนมากเป็นปัญหาสำคัญสำหรับเราพ่อค้า";
			mes "ฉันได้เสนอแผนใหม่ให้กับกิลด์ของเราโดยอาศัยประสบการณ์ 10 ปีในการซื้อสินค้า";
			next;
			mes "[Mr. Hugh]";
			mes "'เปิดร้านผู้ซื้อจำนวนมาก!'";
			mes "นั่นคือชื่อของข้อเสนอของฉัน";
			mes "คุณจะเห็นเมื่อคุณอ่านมัน...";
			next;
			mes "[Mr. Hugh]";
			mes "...";
			mes "...(Mr. Hugh yammers เล่ารายละเอียดทั้งหมด)";
			next;
			select("เอาล่ะประเด็นของคุณคืออะไร?");
			mes "[Mr. Hugh]";
			mes "อ๋อใช่สรุปแล้ว";
			mes "คุณสามารถซื้อสินค้าบางรายการในจำนวนมากผ่านการจำหน่ายสินค้าอัตโนมัติ";
			next;
			mes "[Mr. Hugh]";
			mes "ในการเปิด Bulk Buyer Shop คุณต้องมีใบอนุญาตที่ออกจาก Merchant Guild";
			next;
			mes "[Mr. Hugh]";
			mes "คุณต้องใช้ทุกครั้งที่เปิดร้านเราคาดหวังว่าผลกำไรจะเพิ่มขึ้นอย่างมากจากการออกใบอนุญาตรูปแบบใหม่นี้";
			next;
			mes "[Mr. Hugh]";
			mes "นั่นคือประเด็นของข้อเสนอของฉัน!";
			mes "ประธานของเราดีใจมากที่ได้ทราบว่าเราจะทำเงินได้มาก!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "เข้าประเด็นแล้ว!";
			next;
			mes "[Mr. Hugh]";
			mes "อย่าเพิ่งใจร้อนได้มั้ย?";
			mes "ประเด็นของฉันคือเราสามารถให้คุณเปิด Bulk Buyer Shop ได้ถ้าคุณได้เรียนรู้ Vending";
			next;
			mes "[Mr. Hugh]";
			mes "แน่นอนเราคิดค่าลงทะเบียนครั้งเดียว 10,000 zeny";
			mes "คุณจะต้องมี ^4A4AFFBulk Buyer Shop License^000000 ทุกครั้งที่คุณเปิดร้าน";
			next;
			mes "[Mr. Hugh]";
			mes "บอกว่าอยากลงทะเบียนเลยไหม";
			mes "ถ้าเป็นเช่นนั้นฉันจะสอนวิธีเปิด Bulk Buyer Shop";
			next;
			if(select("เรียนรู้วิธีเปิด Bulk Buyer Shop:ยกเลิก") == 2) {
				mes "[Mr. Hugh]";
				mes "ผู้ชายที่น่าผิดหวัง!";
				mes "การใช้ตัวเลือกผู้ซื้อจำนวนมากนี้จะเป็นประโยชน์ต่อธุรกิจของคุณในหลายๆ ด้าน แต่เป็นการเรียกร้องของคุณ";
				mes "ยินดีให้คำปรึกษาเสมอ!";
				close;
			}
			mes "[Mr. Hugh]";
			if (Zeny < 10000) {
				mes "ค่าลงทะเบียน 10,000 zeny";
				mes "กรุณาเตรียมค่าธรรมเนียมให้พร้อมก่อน";
				close;
			} else {
				mes "คุณตัดสินใจดีแล้ว";
				mes "กรุณาแจ้งค่าลงทะเบียนและลงชื่อตรงนี้....";
				next;
				input .@name$;
				mes "[Mr. Hugh]";
				mes .@name$+"....";
				mes "ฉันชอบลายมือของคุณ";
				mes "โอเคคุณได้รับอนุมัติให้เปิด Bulk Buyer Shop แล้ว";
				set Zeny,Zeny-10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,SKILL_PERM_GRANT;
				next;
				mes "[Mr. Hugh]";
				mes "ปัจจุบันมีเพียงไอเทมธรรมดา ^8C2121ยกเว้นอุปกรณ์^000000 ยาบางชนิดและไอเท็มที่สร้างขึ้นด้วยมือเท่านั้นที่สามารถซื้อได้จำนวนมาก แต่ก็ยังมีประโยชน์ต่อคุณมากขึ้นอยู่กับวิธีการใช้งานของคุณ";
				next;
				mes "[Mr. Hugh]";
				mes "โอ้คุณต้องมีสินค้าอย่างน้อยหนึ่งชิ้นที่คุณต้องการซื้อในคลังของคุณเพราะคุณต้องแสดงให้คนอื่นเห็นผ่านทางร้านค้าของคุณ";
				next;
				mes "[Mr. Hugh]";
				mes "ทักษะของคุณควรได้รับการลงทะเบียนในหน้าต่างทักษะของคุณแล้วหากคุณมองไม่เห็นคุณอาจต้องย่อขนาดรายการทักษะของคุณและตรวจสอบแท็บงานที่ 3";
				next;
				mes "[Mr. Hugh]";
				mes "ฉันให้ใบอนุญาตร้านค้าผู้ซื้อจำนวนมาก 5 ใบสำหรับการทดลองใช้ของคุณ";
				mes "ช้อปเพลิน!";
				close;
			}
		}
	}
	mes "ฉันเป็นฮิวจ์จากทีมจัดซื้อที่ Alberta Merchant Guild";
	mes "ฉันอยากคุยด้วย แต่ตอนนี้ฉันยุ่งเกินไป";
	next;
	mes "[Mr. Hugh]";
	mes "เวลาของฉันทุ่มเทให้กับลูกค้าในอุตสาหกรรมการค้า แต่เพียงผู้เดียว";
	close;
}
