//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================ 

// PecoPeco Breeder (for Knights) :: breeder
//============================================================
prontera,55,350,5	script	Peco Peco Breeder#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "ยินดีต้อนรับ";
		mes "อัศวินผู้มีเกียรติคุณต้องการเช่า Peco Peco หรือไม่";
		mes "ค่าเช่า " +.@price + " zeny.";
		next;
		switch(select("เช่า Peco Peco:ยกเลิก")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "คุณมี zeny ไม่พอ";
				mes "คุณ...ล้มละลายหรือเปล่า";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "ฉันขอโทษ แต่คุณไม่มีสิทธิ์ใช้บริการนี้";
				mes "กรุณาไปเรียนรู้ทักษะ Peco Peco Ride ก่อน";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "คุณได้ติดตั้ง Peco Peco แล้ว";
				close;
			}
			else if(ismounting()) {
				mes "[Peco Peco Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Peco Peco Breeder]";
			mes "ฉันเข้าใจ";
			mes "ถ้าอย่างนั้นขอให้มีความสุขในวันนี้";
			close;
		}
	} else {
		mes "ฉันขอโทษ แต่ Peco Pecos เหล่านี้มีให้สำหรับ Knights และ Lord Knights เท่านั้น";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)
//============================================================
prontera,232,318,3	script	Peco Peco Breeder#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[PecoPeco Breeder]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "ยินดีต้อนรับ, Crusader.";
		else mes "ยินดีต้อนรับ, Paladin.";
		mes "เรามี Peco Peco แบบพิเศษเตรียมไว้สำหรับคุณหากต้องการเช่าจะมีราคา " +.@price + " zeny.";
		next;
		switch(select("เช่า PecoPeco:ออก")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "คุณมี zeny ไม่พอ";
				mes "หากต้องการ Peco Peco กรุณานำ " +.@price + " zeny...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "ก่อนอื่นคุณต้องเรียนรู้ที่จะขี่ PecoPeco ก่อนถึงจะเช่ารถให้คุณได้";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "คุณได้ติดตั้ง Peco Peco แล้ว";
				close;
			}
			else if(ismounting()) {
				mes "[Peco Peco Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[PecoPeco Breeder]";
			mes "เจอกันใหม่";
			close;
		}
	} else {
		mes "ให้ฉันทำอะไรให้";
		mes "โปรดทราบว่าบริการเช่ารถ Peco Peco นี้มีไว้สำหรับพวก Crusaders และ Paladins เท่านั้น";
		close;
	}
}

// Falcon Master :: breeder
//============================================================
hu_in01,381,304,5	script	Falcon Breeder#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Falcon Breeder]";
	if (BaseJob == Job_Hunter) {
		if (checkwug()) {
			mes "อืม...";
			mes "คุณไม่สามารถเช่าเหยี่ยวได้จนกว่าคุณจะเลิกใช้ warg ก่อน!";
			close;
		}
		mes "คุณต้องการเหยี่ยวหรือไม่";
		mes "เช่านกล่าเหยื่อคู่ใจได้ในราคาเพียง " +.@price + " zeny~";
		next;
		switch(select("เช่าเหยี่ยว:ยกเลิก")) {
		case 1:
			if (checkwug()) {
				mes "[Falcon Breeder]";
				mes "อืม...";
				mes "คุณไม่สามารถเช่าเหยี่ยวได้จนกว่าคุณจะเลิกใช้ warg ก่อน!";
				close;
			}
			if (Zeny < .@price) {
				mes "[Falcon Breeder]";
				mes "นี่คืออะไร";
				mes "คุณมี zeny ไม่พอเหรอ!";
				mes "เริ่มล่าเงินแทนมอนสเตอร์ดีกว่า~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Falcon Breeder]";
				mes "เอ้ย~";
				mes "ไปเรียนรู้วิธีจัดการเหยี่ยวก่อน!";
				mes "ฉันไม่สามารถเช่าให้คุณได้ถ้าคุณไม่สามารถจัดการได้คุณก็รู้";
				close;
			}
			else if(checkfalcon()) {
				mes "[Falcon Breeder]";
				mes "อืม... ";
				mes "คุณมีเหยี่ยวอยู่แล้วมันอยู่ตรงนั้นไม่เห็นเหรอ?";
				close;
			}
			set Zeny, Zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Falcon Breeder]";
			mes "รอ - เดี๋ยวก่อนคุณจะไปไหน";
			mes "ฟอลคอนพวกนี้สุดยอดมากรับรองได้เลย! C'mon, yo ~";
			close;
		}
	} else {
		mes "ยัยโง่!";
		mes "เหยียวใช้ได้เฉพาะกับ Hunters และ Snipers เท่านั้น";
		mes "...หึหึอิจฉา?";
		close;
	}
}
