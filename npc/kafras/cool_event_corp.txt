//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[Cool Event Corp. Staff]";
	mes "ยินดีต้อนรับสู่ Cool Event Corp. ";
	mes "พนักงานของเราทำงานตลอดเวลา";
	mes "เพื่อให้เกินกว่าประสบการณ์ของคุณ";
	mes "เพื่อการบริการที่มีคุณภาพดังนั้นอย่างไร";
	mes "วันนี้ให้ฉันช่วยไหม?";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[Cool Event Corp. Staff]";
		mes "จุดเกิดใหม่ของคุณ";
		mes "ถูกบันทึกไว้ที่นี่";
		mes getarg(1)+".";
		mes "ขอบคุณที่ใช้";
		mes "บริการ Cool Event Corp. ~";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close2;
		break;
	case 2:
		mes "[Cool Event Corp. Staff]";
		if(!callfunc("F_CanOpenStorage")){
			mes "ฉันขอโทษ แต่คุณ";
			mes "ต้องการของสามเณร";
			mes "ทักษะพื้นฐานระดับ 6 ถึง";
			mes "ใช้บริการจัดเก็บข้อมูล";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "ฉันขอโทษ แต่คุณไม่มี Zeny เพียงพอที่จะใช้";
			mes "บริการจัดเก็บของเรา";
			mes "ค่าธรรมเนียมการเข้าถึงพื้นที่เก็บของ 40 Zeny";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "ให้ฉันเปิดส่วนตัวของคุณ";
		mes "พื้นที่เก็บข้อมูลสำหรับคุณทันที";
		mes "ขอบคุณที่สนับสนุน Cool Event Corp โดยใช้บริการขอให้มีความสุข ~";
		callfunc("F_CheckKafCode");	//check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "กรุณาเลือก";
		mes "ปลายทางของคุณ";
		next;
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
			set .@cost,4400;
		else
			set .@cost,2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ +" -> "+ .@cost +" z:ยกเลิก") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (.@destination$ == "Veins") warp "veins",205,101;
				else if (.@destination$ == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[Cool Event Corp. Staff]";
				mes "ฉันขอโทษ แต่คุณไม่มี";
				mes "zeny เพียงพอสำหรับการบริการเทเลพอร์ต";
				mes "ค่าธรรมเนียมในการเทเลพอร์ต";
				mes "ถึง " + .@destination$ + " คือ " + .@cost + " zeny.";
			}
		}
		close2;
		break;
	case 4:
		mes "[Cool Event Corp. Staff]";
		if(BaseClass != Job_Merchant){
			mes "ฉันขอโทษ";
			mes "บริการให้เช่ารถเข็น";
			if (checkre(0))
				mes "ใช้ได้เฉพาะกับคลาส Merchant";
			else {
				mes "มีให้เฉพาะ Merchants,";
				mes "Blacksmiths, White Smiths,";
				mes "Alchemists และ Creators.";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "คุณสามารถเช่ารถเข็นได้หลัง";
			mes "การเรียนรู้ทักษะรถเข็น";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "คุณมีรถเข็นติดตั้งอยู่แล้ว";
			mes "น่าเสียดายที่เราทำไม่ได้";
			mes "เช่ามากกว่าหนึ่งรายให้กับลูกค้าในแต่ละครั้ง";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "เช่ารถเข็น";
		mes "ค่าธรรมเนียม 800 Zeny";
		mes "คุณต้องการเช่ารถเข็นหรือไม่";
		next;
		if(select("เช่ารถเข็น:ยกเลิก") == 1){
			if(Zeny < 800){
				mes "[Cool Event Corp. Staff]";
				mes "ฉันขอโทษ แต่คุณมีไม่เพียงพอ";
				mes "Zeny จ่ายค่าเช่ารถเข็น 800 Zeny";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Cool Event Corp. Staff]","Cool Event Corp.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[Cool Event Corp. Staff]";
		mes "Cool Event Corp. อยู่เสมอ";
		mes "มุ่งมั่นที่จะมอบสิ่งที่ดีที่สุด";
		mes "บริการสำหรับลูกค้าของเรา";
		mes "ช่วยเราเป็นคนที่ดีที่สุดด้วย";
		mes "ให้ความเห็นของคุณแก่เรา";
		mes "และข้อเสนอแนะที่ตรงไปตรงมาขอบคุณ";
		close2;
	}
	cutin "", 255;
	end;
}

lighthalzen,94,248,3	script	Cool Event Corp Staff::CoolEventCorpStaffLighthalzen	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"บันทึกจุดเกิด:เปิดคลัง::เช่ารถเข็น:บริการรหัสผ่านการจัดเก็บของ:ยกเลิก",
		"ในเมือง Lighthalzen","lighthalzen",95,240;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"บันทึกจุดเกิด:เปิดคลัง::เช่ารถเข็น:บริการรหัสผ่านการจัดเก็บของ:ยกเลิก",
		"in the village of Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"บันทึกจุดเกิด:เปิดคลัง:ใช้บริการเทเลพอต:เช่ารถเข็น:บริการรหัสผ่านการจัดเก็บของ:ยกเลิก",
		"ในเมือง Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"บันทึกจุดเกิด:เปิดคลัง:ใช้บริการเทเลพอต:เช่ารถเข็น:บริการรหัสผ่านการจัดเก็บของ:ยกเลิก",
		"ในเมือง Veins","veins",204,103,"Rachel";
	end;
}
